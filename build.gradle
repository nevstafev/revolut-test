plugins {
    id 'java'
}

group 'home'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    compile 'org.glassfish.grizzly:grizzly-http-server:2.3.30'
    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.25.1'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.25.1'
    integrationTestCompile 'org.glassfish.jersey.core:jersey-client:2.25.1'

}
